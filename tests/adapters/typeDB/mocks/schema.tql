define 

# Shared
id sub attribute, value string;
name sub attribute, value string;
email sub attribute, abstract, value string, regex '^(?=.{1,64}@)[A-Za-z0-9_-]+(\\.[A-Za-z0-9_-]+)*@[^-][A-Za-z0-9-]+(\\.[A-Za-z0-9-]+)*(\\.[A-Za-z]{2,})$';
description sub attribute, value string;


Thing·stuff sub attribute, value string;
ThingRelation·moreStuff sub attribute, value string;


# User
User·email sub email;

# SuperUser
SuperUser·power sub attribute, value string;

# God
God·isEvil sub attribute, value boolean;

# Kind
Kind·name sub attribute, value string;

# Field
Field·cardinality sub attribute, value string;
Field·name sub attribute, value string;

# DataField
DataField·computeType sub attribute, value string;
DataField·type sub attribute, value string;
DataValue·type sub attribute, value string;


# Session
Session·expires sub attribute,
    value datetime;
Session·sessionToken sub attribute,
    value string;


Expression·value sub attribute,
    value string;

Expression·type sub attribute,
    value string;

Hook·timestamp sub attribute,
		value datetime;

Hook·requiredOption sub attribute,
		value string,
		regex "^(a|b|c)$";

Hook·manyOptions sub attribute,
		value string,
		regex "^(a|b|c)$";

Hook·fnValidatedField sub attribute,
		value string;

Space sub entity,
    owns id @key,
    owns name,
		plays SpaceObj:space,
    plays Space-User:spaces,
    plays UserTagGroup:space;

#Simple Things

Thing sub entity,
    owns id@key,
    owns Thing·stuff,
    plays ThingRelation:root,
    plays ThingRelation:extra,
    plays ThingRelation:things;

CascadeThing sub entity,
    owns id @key,
    plays CascadeRelation:things;


SubthingOne sub Thing;
SubthingTwo sub Thing;

CascadeRelation sub relation,
    owns id @key,
    relates things;

ThingRelation sub relation,
    owns id @key,
		owns ThingRelation·moreStuff,
    relates root,
    relates extra,
    relates things;


User sub entity,
    owns id @key,
    owns name,
    owns User·email,
    plays User-Accounts:user,
    plays Space-User:users,
    plays UserTag:users,
    plays User-Sessions:user;

SuperUser sub User,
		owns SuperUser·power;

God sub SuperUser,
		owns God·isEvil;

# Account
Account·provider sub attribute, value string;
Account·isSecureProvider sub attribute, value boolean;
Account·profile sub attribute, value string;

Account sub entity,
    owns id @key,
    owns Account·provider,
		owns Account·isSecureProvider,
    owns Account·profile,
    plays User-Accounts:accounts;

# Post
Post·mentions sub attribute, value string;
Post sub entity,
  owns id @key,
  owns Post·mentions;

# UserAccounts
User-Accounts sub relation,
    owns id @key,
    relates user,
    relates accounts;

User-Sessions sub relation,
    owns id @key,
    relates sessions,
    relates user;

    # UserAccounts
Space-User sub relation,
    owns id @key,
    relates users,
    relates spaces,
    relates power;

UserTag sub relation,
    owns id @key,
    owns name,
    relates users,
    plays UserTagGroup:tags;

UserTagGroup sub relation,
  owns id @key,
  relates tags, #cardinality: Many
  relates space, #cardinality: One
  relates color;

Color·name sub attribute, value string;
Color·value sub attribute, value string;
Color·freeForAll sub flexAttribute;

Color sub entity,
  #owns Color·name @key,
  owns id @key,
  owns Color·value,
  owns Color·freeForAll,
  plays UserTagGroup:color;

SpaceObj sub relation,
    owns id @key,
    relates space;

SpaceDef sub SpaceObj,
    owns description;

Kind sub SpaceDef,
    owns Kind·name,
    plays Field:kinds;

Field sub SpaceDef,
    owns Field·cardinality,
    owns Field·name,
    relates kinds;

DataField sub Field,
    plays Expression:dataField,
    plays DataValue:dataField,
    owns DataField·computeType,
    owns DataField·type;

Expression sub relation,
    owns Expression·value,
    owns Expression·type,

    owns id @key,
    relates dataField;
    

DataValue sub relation,
  relates dataField,
  owns DataValue·type,
  owns id @key;


Power sub entity,
    owns id,
    owns description,
    plays Space-User:power;

Self sub SpaceObj,
    relates owner,
    plays Self:owner;

Session sub entity,
    owns Session·sessionToken @key,
    owns id @key,
    owns Session·expires,
    plays User-Sessions:sessions;

Hook sub entity,
	owns id @key,
	owns Hook·timestamp,
	owns Hook·requiredOption,
	owns Hook·manyOptions,
	owns Hook·fnValidatedField,
	plays HookParent:mainHook,
	plays HookParent:hooks,
	plays HookATag:hookTypeA,
	plays HookATag:otherHooks;

HookParent sub relation,
	owns id @key,
	relates hooks,
	relates mainHook;

HookATag sub relation,
	owns id @key,
	relates hookTypeA,
	relates otherHooks;

#1 Secure provider rule
rule add-secure-provider: when {
	  $account isa Account, has Account·provider "google";
} then {
	  $account has Account·isSecureProvider true;
};

rule add-non-secure-provider: when {
	  $account isa Account;
		not { $account has Account·isSecureProvider true; };
} then {
	  $account has Account·isSecureProvider false;
};


#2 With parent tag rule	
rule add-hook-type-a-tag: when {
	  $hook isa Hook, has Hook·requiredOption "a";
		$otherHooks isa Hook;
		not { $hook is $otherHooks; };
		not { $otherHooks has Hook·requiredOption "a"; };
	
} then {
	  (hookTypeA: $hook, otherHooks: $otherHooks) isa HookATag;
};
	
	
#Tools, reserved for every schema using borm

  stringAttribute sub attribute,
    value string;

  longAttribute sub attribute,
    value long;

  doubleAttribute sub attribute,
    value double;

  booleanAttribute sub attribute,
    value boolean;

  datetimeAttribute sub attribute,
    value datetime;

  flexAttribute sub attribute, abstract, value string,
    owns stringAttribute,
    owns longAttribute,
    owns doubleAttribute,
    owns booleanAttribute,
    owns datetimeAttribute;
    
